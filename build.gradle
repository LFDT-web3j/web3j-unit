plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'de.undercouch.download' version '4.1.2'
    id 'org.jreleaser' version '1.19.0'
}

description 'Web3j-unit extension'


ext {
    web3jVersion = version
    kotlinLoggingVersion = '3.0.5'
    logbackVersion = '1.5.6'
    mockitoKotlinVersion = '1.9.24'
    testContainersVersion = '1.19.8'
    besuPluginVersion = '25.2.1'
    besuInternalVersion = '25.2.1'
    besuInternalCryptoVersion = '23.1.3'
    besuCryptoDepVersion = '1.1.2'
    besuBlsVersion = '1.0.0'
}

apply {
    [
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
        'junit'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/LFDT-web3j/web3j-build-tools/main/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite false
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

repositories {
    mavenCentral()
    maven { url "https://hyperledger.jfrog.io/artifactory/besu-maven/" }
    maven { url "https://artifacts.consensys.net/public/maven/maven/" }
    maven { url "https://splunk.jfrog.io/splunk/ext-releases-local" }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.web3j:core:$web3jVersion"

    implementation "org.web3j:web3j-evm:$web3jVersion"

    implementation "org.testcontainers:testcontainers:$testContainersVersion"
    implementation "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    implementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    implementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    implementation "org.hyperledger.besu:plugin-api:$besuPluginVersion"
    implementation("org.hyperledger.besu.internal:besu:$besuInternalVersion") {
        exclude group: 'org.web3j'
    }
    implementation("org.hyperledger.besu.internal:api:$besuInternalVersion") {
        exclude group: 'org.web3j'
    }
    implementation "org.hyperledger.besu:evm:$besuPluginVersion"
    implementation "org.hyperledger.besu.internal:config:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:core:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:crypto:$besuInternalCryptoVersion"
    implementation "org.hyperledger.besu.internal:rlp:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:kvstore:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:metrics-core:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:trie:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:util:$besuInternalVersion"
    implementation "org.hyperledger.besu:bls12-381:$besuBlsVersion"

    implementation "org.hyperledger.besu:secp256k1:$besuCryptoDepVersion"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "21"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "21"
}


tasks.named("spotlessKotlin").configure { dependsOn("compileKotlin","spotlessJava", "spotlessGroovyGradle","processTestResources") }
tasks.named("spotlessJava").configure { dependsOn("compileTestJava","compileTestKotlin","compileKotlin","compileTestKotlin","spotlessGroovyGradle","processTestResources") }
tasks.named("test").configure { dependsOn("spotlessKotlin","spotlessJava") }
